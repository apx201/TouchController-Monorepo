load("@rules_java//java:defs.bzl", "java_binary", "java_library")
load("//rule:apply_access_widener.bzl", "apply_access_widener")
load("//rule:decompile_jar.bzl", "decompile_jar")
load("//rule:extract_access_widener.bzl", "extract_access_widener")
load("//rule:generate_remap_classpath.bzl", "generate_remap_classpath")
load("//rule:remap_jar.bzl", "remap_jar")
load("//rule/fabric:merge_jij.bzl", "fabric_merge_jij")

remap_jar(
    name = "remapped_deps",
    classpath = [
        "//game/1.21.8:game_client_intermediary",
    ],
    from_namespace = "intermediary",
    inputs = ["@maven//:net_fabricmc_fabric_api_fabric_api"],
    mapping = "//game/1.21.8:game_merged_mapping",
    mixin = True,
    remap_transitive_deps = True,
    remove_jar_in_jar = True,
    to_namespace = "named",
)

extract_access_widener(
    name = "access_widener_dep_named",
    inputs = [":remapped_deps"],
)

apply_access_widener(
    name = "remapped_client_access_widened_named",
    srcs = [":access_widener_dep_named"],
    input = "//game/1.21.8:game_client_named",
)

decompile_jar(
    name = "decompile_client_access_widened_named",
    inputs = [":remapped_client_access_widened_named"],
)

java_library(
    name = "ball_block_unmapped",
    srcs = glob(["src/main/java/**/*.java"]),
    resources = glob(["src/main/resources/**"]),
    runtime_deps = [
        "//blazerod:blazerod_fabric_without_jij",
    ],
    deps = [
        ":remapped_client_access_widened_named",
        ":remapped_deps",
        "//blazerod/model/model-base",
        "//blazerod/model/model-formats",
        "//blazerod/render/api/loader",
        "//blazerod/render/api/render",
        "//blazerod/render/api/resource",
        "@maven//:net_fabricmc_fabric_loader",
        "@minecraft//1.21.8:client_libraries",
    ],
)

remap_jar(
    name = "ball_block_without_jij",
    classpath = ["//game/1.21.8:game_client_named"],
    from_namespace = "named",
    inputs = [":ball_block_unmapped"],
    mapping = "//game/1.21.8:game_merged_mapping",
    mixin = True,
    to_namespace = "intermediary",
)

fabric_merge_jij(
    name = "ball_block",
    input = ":ball_block_without_jij",
    deps = {
        "//blazerod/model/model-base": "blazerod-model-base:=",
        "//blazerod/model/model-formats": "blazerod-model-formats:=",
        "//blazerod/model/model-gltf": "blazerod-model-formats-gltf:=",
        "//blazerod/model/model-pmd": "blazerod-model-formats-pmd:=",
        "//blazerod/model/model-pmx": "blazerod-model-formats-pmx:=",
        "//blazerod/model/model-vmd": "blazerod-model-formats-vmd:=",
        "//blazerod/render:render_fabric_merged": "blazerod-render:=",
    },
)

generate_remap_classpath(
    name = "remap_classpath",
    prefix = "../../../../../../",
    deps = [
        ":remapped_deps",
        "//game/1.21.8:game_client_intermediary",
    ],
)

java_binary(
    name = "client",
    srcs = [],
    data = [
        ":remap_classpath",
        "@minecraft_assets//:assets",
        "@minecraft_assets//:assets_1.21.8",
    ],
    jvm_flags = [
        "-Dfabric.development=true",
        "-Dfabric.remapClasspathFile=$(rlocationpath :remap_classpath)",
        "-Ddev.launch.version=1.21.8",
        "-Ddev.launch.type=client",
        "-Ddev.launch.assetsPath=$(rlocationpath @minecraft_assets//:assets)",
        "-Ddev.launch.mainClass=net.fabricmc.loader.impl.launch.knot.KnotClient",
        "-Ddev.launch.expandRunfileProperties=fabric.remapClasspathFile",
        "-Dblazerod.debug=true",
        "-Dblazerod.debug.gui=true",
        "-Xmx4G",
    ],
    main_class = "top.fifthlight.fabazel.devlaunchwrapper.DevLaunchWrapper",
    runtime_deps = [
        ":ball_block_unmapped",
        ":remapped_deps",
        "//blazerod:blazerod_fabric_without_jij",
        "//game/1.21.8:game_client_named",
        "//rule/dev_launch_wrapper",
        "@maven//:io_github_llamalad7_mixinextras_fabric",
        "@maven//:net_fabricmc_fabric_language_kotlin",
        "@maven//:net_fabricmc_fabric_loader",
        "@maven//:net_fabricmc_sponge_mixin",
        "@maven//:net_fabricmc_yarn_v2",
        "@minecraft//1.21.8:client_libraries",
    ],
)
